plugins {
    id 'java'
}

group 'org.mentalizr'
version = rootProject.file('version').text.trim()

repositories {
    mavenCentral()
    maven { url "https://raw.githubusercontent.com/arthurpicht/mvn-repo/main/releases"}
}

dependencies {
    implementation libs.m7r.contentManager
    implementation libs.m7r.mdpCompiler
    implementation libs.m7r.serviceObjects

    implementation libs.ap.cli
    implementation libs.ap.utils.core
    implementation libs.ap.utils.io

    implementation libs.jakarta.xml.bind.api
    implementation libs.jersey.media.json.binding
    implementation libs.jcolor

    testImplementation libs.junit.jupiter
    testRuntimeOnly libs.junit.engine
}

test {
    useJUnitPlatform()
}

tasks.register('extractDependencies', Copy) {
    dependsOn jar
    from configurations.runtimeClasspath
    into "$buildDir/dependencies"
}

tasks.register('fatJar', Jar) {
    dependsOn extractDependencies
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes 'Main-Class': 'org.mentalizr.contentManagerCli.ContentManagerCli'
        attributes 'Version': project.property("version")
        attributes 'Build': new Date().format('yyyyMMddHHmmss')
    }
    from {
        file("$buildDir/dependencies").listFiles().collect { zipTree(it) }
    }
    from {
        file("$buildDir/libs").listFiles().collect { zipTree(it) }
    }
    destinationDirectory.set(file("$buildDir/fatjar"))
}

tasks.register('tool') {
    dependsOn fatJar
}

defaultTasks 'fatJar'
